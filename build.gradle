plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'maven-publish'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'io.nodelink.server.NodeLink'
    applicationDefaultJvmArgs = ["--enable-native-access=ALL-UNNAMED"]
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-native-access=ALL-UNNAMED"
}

task generateVersionClass {
    doLast {
        def versionFile = file("$buildDir/generated/source/version/io/nodelink/server/update/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package io.nodelink.server.update;

public class Version {
    public static final String VERSION = "${project.version}";
}
"""
    }
}

compileJava.dependsOn generateVersionClass
sourceSets.main.java.srcDir "$buildDir/generated/source/version"

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

shadowJar {
    archiveClassifier.set('fat')

    manifest {
        attributes 'Main-Class': 'io.nodelink.server.NodeLink'
    }
}

group = 'io.nodelink.server'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    implementation platform('io.projectreactor:reactor-bom:2022.0.21')

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jline:jline:3.30.0'
    implementation 'io.projectreactor:reactor-core:3.8.1'
    implementation 'io.projectreactor.netty:reactor-netty-core'
    implementation 'io.projectreactor.netty:reactor-netty-http'
    implementation 'io.javalin:javalin:6.7.0'
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'

    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    implementation 'org.reflections:reflections:0.10.2'
}

test {
    useJUnitPlatform()
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'io.nodelink.server.NodeLink'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group.toString()
            artifactId = 'NodeLink-Server'
            version = project.version.toString()
            pom {
                name = 'NodeLink-Server'
                description = 'Change later'
                url = 'https://nodelink-project.github.io/NodeLink-Server/'
                licenses {
                    license {
                        name = 'GPL-3.0'
                        url = 'https://raw.githubusercontent.com/NodeLink-Project/NodeLink-Server/refs/heads/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sandro642'
                        name = 'Sandro'
                        email = 'sandro33810@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/NodeLink-Project/NodeLink-Server.git'
                    developerConnection = 'scm:git:ssh://github.com/NodeLink-Project/NodeLink-Server.git'
                    url = 'https://github.com/NodeLink-Project/NodeLink-Server'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'LocalRepo'
            url = uri(layout.buildDirectory.dir('repo'))
        }
    }
}